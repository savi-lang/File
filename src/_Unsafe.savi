:: These are the low-level file operations that are used by the higher-level
:: file actors in this library. They are slightly higher-level than raw FFI,
:: but they still do not have safety guardrails of their own, and they rely
:: on the calling code to call carefully, without invalid arguments.
::
:: These low-level operations also do not enforce capability security, and
:: most of them do not take file path capability object as an argument,
:: so they rely on the caller to have already enforced that requirement.
::
:: In case of any error, the functions in this module raise an error code.
:module _Unsafe
  :: Check that the given path string is valid for use with FFI.
  :fun check_path!(path_string String)
    :errors File.Result

    // If the path string contains a null byte, C APIs that don't take a length
    // will stop reading the string at that byte, such that it wouldn't be
    // using the full path string the user requested. In such a case, we
    // should reject the path as invalid rather than open the wrong path.
    if path_string.includes("\0") (
      error! File.Result.PathInvalid
    )

  :: Open a file at the given path for reading, if it exists.
  ::
  :: On success, returns a valid file descriptor ready for reading.
  :: Otherwise, an error result code is raised.
  :fun open_read!(path File.Path.Readable)
    :errors File.Result

    fd = _FFI.open(path.string.cstring, _FFI.o_rdonly, 0)
    if fd == -1 (
      case _FFI.errno == (
      | OSError.EACCES       | error! File.Result.AccessDenied
      | OSError.EFBIG        | error! File.Result.FileIsTooLarge
      | OSError.EINVAL       | error! File.Result.PathInvalid
      | OSError.EISDIR       | error! File.Result.PathIsDirectory
      | OSError.ELOOP        | error! File.Result.TooManySymbolicLinksFollowed
      | OSError.EMFILE       | error! File.Result.TooManyFilesOpenInProcess
      | OSError.ENAMETOOLONG | error! File.Result.PathTooLong
      | OSError.ENFILE       | error! File.Result.TooManyFilesOpenInSystem
      | OSError.ENOENT       | error! File.Result.PathNotFound
      | OSError.ENODEV       | error! File.Result.FileIsNonOpenableDevice
      | OSError.ENOMEM       | error! File.Result.MemoryBufferExhausted
      | OSError.ENXIO        | error! File.Result.FileIsNonOpenableDevice
      | OSError.EOVERFLOW    | error! File.Result.FileIsTooLarge
      | OSError.EPERM        | error! File.Result.FileIsSealed
      | OSError.EAGAIN       | error! File.Result.FileIsLockedPseudoTerminal // weird MacOS use of EAGAIN...
      | OSError.EINTR        | error! File.Result.NeedsRetry
      |                        error! File.Result.BugInLibrary
      )
    )
    fd

  :: Open a file at the given path for writing, creating it if needed.
  ::
  :: On success, returns a valid file descriptor ready for writing.
  :: Otherwise, an error result is raised.
  :fun open_write_create!(path File.Path.Writable)
    :errors File.Result

    default_mode = case (
    | Platform.is_posix   | 0b111111101 // rwxrwxr-x
    | Platform.is_windows | 0x0080 // _S_IWRITE
    |                       0
    )
    // TODO: implement a way to let the caller affect the mode/permissions,
    // after deciding a few key questions:
    //   - how will we handle stark differences between POSIX and Windows?
    //   - should we apply these permissions only if the file is created,
    //     or also if already existed and we are just overwriting it?

    fd = _FFI.open(
      path.string.cstring
      _FFI.o_wronly
        .bit_or(_FFI.o_creat)
        .bit_or(_FFI.o_trunc)
        .bit_or(_FFI.o_binary) // relevant to windows only
      default_mode
    )
    if fd == -1 (
      case _FFI.errno == (
      | OSError.EACCES       | error! File.Result.AccessDenied
      | OSError.EDQUOT       | error! File.Result.DiskQuotaExhausted
      | OSError.EFBIG        | error! File.Result.FileIsTooLarge
      | OSError.EINVAL       | error! File.Result.PathInvalid
      | OSError.EISDIR       | error! File.Result.PathIsDirectory
      | OSError.ELOOP        | error! File.Result.TooManySymbolicLinksFollowed
      | OSError.EMFILE       | error! File.Result.TooManyFilesOpenInProcess
      | OSError.ENAMETOOLONG | error! File.Result.PathTooLong
      | OSError.ENFILE       | error! File.Result.TooManyFilesOpenInSystem
      | OSError.ENODEV       | error! File.Result.FileIsNonOpenableDevice
      | OSError.ENOMEM       | error! File.Result.MemoryBufferExhausted
      | OSError.ENOSPC       | error! File.Result.DiskSpaceExhausted
      | OSError.ENOTDIR      | error! File.Result.PathPrefixIsNotDirectory
      | OSError.ENXIO        | error! File.Result.FileIsNonOpenableDevice
      | OSError.EOVERFLOW    | error! File.Result.FileIsTooLarge
      | OSError.EPERM        | error! File.Result.FileIsSealed
      | OSError.EROFS        | error! File.Result.FileSystemIsReadOnly
      | OSError.ETXTBSY      | error! File.Result.FileIsBusy
      | OSError.EAGAIN       | error! File.Result.FileIsLockedPseudoTerminal // weird MacOS use of EAGAIN...
      | OSError.ENOENT       | error! File.Result.PathPrefixNotFound
      | OSError.EINTR        | error! File.Result.NeedsRetry
      |                        error! File.Result.BugInLibrary
      )
    )
    fd

  :fun stat_as_file_info!(path File.Path.Any) File.Info
    :errors File.Result

    try (
      _FFI.Stat.stat_as_file_info!(path)
    |
      case _FFI.errno == (
      | OSError.success      | error! File.Result.BugInLibrary // (unsupported platform in the above function)
      | OSError.EACCES       | error! File.Result.AccessDenied
      | OSError.ELOOP        | error! File.Result.TooManySymbolicLinksFollowed
      | OSError.ENAMETOOLONG | error! File.Result.PathTooLong
      | OSError.ENOENT       | error! File.Result.PathNotFound
      | OSError.ENOTDIR      | error! File.Result.PathNotFound
      | OSError.ENOMEM       | error! File.Result.MemoryBufferExhausted
      | OSError.EOVERFLOW    | error! File.Result.FileIsTooLarge
      | OSError.EIO          | error! File.Result.LowLevelIOFailed
      | OSError.EAFNOSUPPORT | error! File.Result.LowLevelCorruption // TODO: called EINTEGRITY on FreeBSD - need to eventually patch OSError in some way to include this
      |                        error! File.Result.BugInLibrary
      )
    )

  :fun fstat_as_file_info!(path File.Path.Any, fd I32) File.Info
    :errors File.Result

    try (
      _FFI.Stat.fstat_as_file_info!(path, fd)
    |
      case _FFI.errno == (
      | OSError.success      | error! File.Result.BugInLibrary // (unsupported platform in the above function)
      | OSError.EACCES       | error! File.Result.AccessDenied
      | OSError.ENOMEM       | error! File.Result.MemoryBufferExhausted
      | OSError.EOVERFLOW    | error! File.Result.FileIsTooLarge
      | OSError.EIO          | error! File.Result.LowLevelIOFailed
      | OSError.EAFNOSUPPORT | error! File.Result.LowLevelCorruption // TODO: called EINTEGRITY on FreeBSD - need to eventually patch OSError in some way to include this
      |                        error! File.Result.BugInLibrary
      )
    )

  :fun read!(fd I32, buffer Bytes'ref) Bool
    :errors File.Result

    space = buffer.space - buffer.size
    if space == 0 (
      buffer.reserve(buffer.space + 1) // reserve the next exponential size up
      space = buffer.space - buffer.size
    )

    read_bytes = _FFI.read(fd, buffer.cpointer.offset(buffer.size), space)
    if read_bytes == -1 (
      case _FFI.errno == (
      | OSError.EIO     | error! File.Result.LowLevelIOFailed
      | OSError.EISDIR  | error! File.Result.PathIsDirectory
      | OSError.ENOBUFS | error! File.Result.MemoryBufferFailed
      | OSError.ENOMEM  | error! File.Result.MemoryBufferExhausted
      | OSError.ENXIO   | error! File.Result.FileIsNonReadableDevice
      | OSError.EINTR   | return False // keep reading, trying again
      | 0               | None // TODO: this unreachable case shouldn't be necessary
      |                   error! File.Result.BugInLibrary
      )
    )
    buffer.resize_possibly_including_uninitialized_memory(
      buffer.size + read_bytes.usize
    )
    read_bytes == 0 // return True if we've finished reading the entire file

  :fun read_all!(path File.Path.Readable, fd I32, buffer Bytes'ref) None
    :errors File.Result

    // Get the file size to create an buffer of the right initial size, then
    // read all of the file into the buffer, taking into account the
    // edge case where the file has changed size since it was last measured
    // by the earlier stat call.
    //
    // That is, measuring the size ahead of time should only be seen as
    // an optimization that prevents re-allocations from a too-small buffer
    // in the common case where the size has not changed since the stat call.
    try (
      initial_stat = @fstat_as_file_info!(path, fd)
      buffer.reserve(initial_stat.size + 1)
    )

    // Now read the entire file into the buffer, one chunk at a time.
    did_finish = False
    while !did_finish (
      did_finish = @read!(fd, buffer)
    )

  :fun writev_loop!(fd I32, incoming_chunks Array(Bytes)'val) None
    :errors File.Result

    // Convert to a C array of POSIX iovec structs.
    bytes_total USize = 0
    chunks = Array(_WriteChunk).new(incoming_chunks.size)
    incoming_chunks.each -> (chunk |
      bytes_total += chunk.size
      if chunk.size > 0 (
        chunks << _WriteChunk.from_cpointer(chunk.cpointer, chunk.size)
      )
    )

    // If there are no bytes to write, we're done.
    return if bytes_total == 0

    // Loop until everything is written.
    while bytes_total > 0 (
      bytes_total = @_writev_inner!(fd, chunks, bytes_total)
    )

  :fun _writev_inner!(
    fd I32
    chunks Array(_WriteChunk)
    bytes_total USize
  ) USize
    :errors File.Result

    bytes_written_isize = case (
    | Platform.is_posix |
      _FFI.writev_posix(
        fd
        chunks.cpointer
        try (chunks.size.i32! | I32.max_value)
      )
    |
      try (
        first_chunk = chunks.first!
        _FFI.write_windows(
          fd
          first_chunk.cpointer
          try (first_chunk.size.u32! | U32.max_value)
        ).isize
      |
        0
      )
    )
    if bytes_written_isize == -1 (
      case _FFI.errno == (
      | OSError.EDQUOT | error! File.Result.DiskQuotaExhausted
      | OSError.EFBIG  | error! File.Result.FileIsTooLarge
      | OSError.EINVAL | error! File.Result.PathInvalid
      | OSError.EIO    | error! File.Result.LowLevelIOFailed
      | OSError.ENOSPC | error! File.Result.DiskSpaceExhausted
      | OSError.EPERM  | error! File.Result.FileIsSealed
      | OSError.EPIPE  | error! File.Result.PipeReaderHasClosed
      | OSError.EINTR  | return bytes_total // needs retry
      | 0              | None // TODO: this unreachable case shouldn't be necessary
      |                  error! File.Result.BugInLibrary
      )
    )
    bytes_written = bytes_written_isize.usize

    // If all chunks were written, we're done.
    return 0 if bytes_total <= bytes_written

    // Otherwise, we need to figure out how much of the original chunks
    // still need to be written - we'll pass them into the next attempt.
    bytes_total -= bytes_written
    chunks_trim_start USize = 0
    first_chunk_trim_start USize = 0
    chunks.each -> (chunk |
      if bytes_written < chunk.size (
        first_chunk_trim_start = bytes_written
        break
      )

      chunks_trim_start += 1
      bytes_written -= chunk.size
      break if bytes_written.is_zero
    )

    // Trim the chunks / parts of chunks that have already been written.
    chunks.trim_in_place(chunks_trim_start)
    if first_chunk_trim_start != 0 (
      try (
        chunks.first! = chunks.first!.trim_left(first_chunk_trim_start)
      )
    )

    // There are still this many more bytes to write.
    bytes_total

  :fun fsync!(fd I32)
    :errors File.Result

    try_again = True
    while try_again (
      try_again = False

      if _FFI.fsync(fd) == -1 (
        case _FFI.errno == (
        | OSError.EIO    | error! File.Result.LowLevelIOFailed
        | OSError.ENOSPC | error! File.Result.DiskSpaceExhausted
        | OSError.EDQUOT | error! File.Result.DiskQuotaExhausted
        | OSError.EROFS  | None // can't fsync a pipe or FIFO - that's okay
        | OSError.EINVAL | None // can't fsync a pipe or FIFO - that's okay
        | OSError.EAGAIN | try_again = True
        | OSError.EINTR  | try_again = True
        |                  error! File.Result.BugInLibrary
        )
      )
    )
    True

  :fun close!(fd I32) None
    :errors File.Result

    failed = _FFI.close(fd) == -1
    if failed (
      case _FFI.errno == (
      | OSError.EIO    | error! File.Result.LowLevelIOFailed
      | OSError.ENOSPC | error! File.Result.DiskSpaceExhausted
      | OSError.EDQUOT | error! File.Result.DiskQuotaExhausted
      | 0              | None // TODO: this unreachable case shouldn't be necessary
      | OSError.EINTR  |
        // POSIX recommends that we don't try again - the file descriptor
        // on Linux is guaranteed to be freed even if interrupted.
        // If we tried again to free it, we might accidentally free
        // a newly allocated file descriptor of the same number.
        // However, the POSIX spec does leave ambiguity for implementations
        // to not free the file descriptor when interrupted. Sad.
        return
      |
        error! File.Result.BugInLibrary
      )
    )
